{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

forall X -> int is_null (X x) asm "ISNULL";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";


() recv_internal() {
}

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id {
  int root_hash =  cell_hash(tree);

  if (root_hash == hash) {
    return tree;
  }

  tuple cells = null();
  cells~push_back(tree);

  while (~ cells.is_null()){
    cell current_cell = cells~pop_back();

    if (cell_hash(current_cell) == hash) {
      return current_cell;
    }

    slice current_cell_slice = current_cell.begin_parse();
    repeat (current_cell_slice.slice_refs()){
      cell current_cell_ref = current_cell_slice~load_ref();

      if (cell_hash(current_cell_ref) == hash) {
        return current_cell_ref;
      }

      cells~push_back(current_cell_ref);
    }
  }

  return begin_cell().end_cell();
}
